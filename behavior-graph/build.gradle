// https://docs.gradle.org/current/samples/sample_building_kotlin_libraries.html
// https://github.com/Kotlin/dokka/blob/master/examples/gradle/dokka-library-publishing-example/build.gradle.kts
// https://docs.gradle.org/current/userguide/signing_plugin.html
// https://www.gnupg.org/gph/en/manual.html

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "maven-publish"
    id "java-library"
    id "signing"
    id "org.jetbrains.dokka"
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_11
    }
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'behaviorgraph')
    }
}

def dokkaJavadocJar = tasks.register("dokkaJavadocJar", org.gradle.jvm.tasks.Jar) {
    it.dependsOn(dokkaJavadoc)
    it.from(dokkaJavadoc.outputDirectory)
    it.archiveClassifier = "javadoc"

}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = releasesRepoUrl //version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                if (project.hasProperty("ossrhUsername")) {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
    publications {
        release(MavenPublication) {
            from components.java
            groupId = 'com.yahoo.behaviorgraph'
            artifactId = 'bgjvm'
            version = '0.8.0'
            artifact dokkaJavadocJar
            repositories {

            }
            pom {
                name = 'Behavior Graph'
                description = 'Behavior Graph lets you build your programs out of small, easily understood pieces in a way that lets the computer do more of the work for you.'
                url = 'https://github.com/yahoo/bgkotlin'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'slevin'
                        name = 'Sean Levin'
                        email = 'slevin@yahooinc.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/yahoo/bgkotlin.git'
                    developerConnection = 'scm:git:ssh://github.com:yahoo/bgkotlin.git'
                    url = 'https://github.com/yahoo/bgkotlin'
                }
            }
        }
    }
}


signing {
    if (project.hasProperty("signing.keyId")) {
        sign configurations.archives
        sign publishing.publications.release
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}
